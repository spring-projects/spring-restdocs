buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE'
	}
}

plugins {
	id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'eclipse'

repositories {
	mavenLocal()
	maven { url 'https://repo.spring.io/snapshot' }
	mavenCentral()
}

group = 'com.example'

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile 'org.springframework.hateoas:spring-hateoas'

	runtime 'com.h2database:h2'
	runtime 'org.springframework.plugin:spring-plugin-core:1.1.0.RELEASE'
	runtime 'org.atteo:evo-inflector:1.2'

	testCompile 'com.jayway.jsonpath:json-path'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.restdocs:spring-restdocs:0.1.0.BUILD-SNAPSHOT'
}

ext {
	generatedDocumentation = file('build/generated-snippets')
}

test {
	systemProperty 'org.springframework.restdocs.outputDir', generatedDocumentation
	outputs.dir generatedDocumentation
}

asciidoctor {
	sourceDir 'src/main/asciidoc' // Align with Maven's default location
	attributes 'generated': generatedDocumentation
	inputs.dir generatedDocumentation
	dependsOn test
}

jar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.3'
}

eclipseJdt.onlyIf { false }
cleanEclipseJdt.onlyIf { false }