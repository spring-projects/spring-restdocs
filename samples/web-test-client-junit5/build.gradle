plugins {
	id 'org.asciidoctor.convert' version '1.5.3'
	id 'org.springframework.boot' version '2.1.3.RELEASE'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
	mavenCentral()
}

group = 'com.example'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
	snippetsDir = file('build/generated-snippets')
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-webflux'
	testCompile('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'junit', module: 'junit'
	}
	testCompile 'io.projectreactor:reactor-test'

	// Spring Rest Docs
	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testCompile 'org.springframework.restdocs:spring-restdocs-webtestclient'

	// Junit 5
	testCompile 'org.junit.jupiter:junit-jupiter-api'
	testCompile 'org.junit.jupiter:junit-jupiter-params'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine'
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform {
		includeEngines 'junit-jupiter'
	}
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

jar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

eclipseJdt.onlyIf { false }
cleanEclipseJdt.onlyIf { false }
